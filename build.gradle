import java.util.regex.Matcher
import java.util.regex.Pattern

project.ext.set('versionName', '0.5.0')
project.ext.set('versionCode', '050')

project.ext.set('archivesBaseName', "BalanduinoAndroidApp-" + project.versionName)

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.5.+'
    }
}

task update { // Task to update accessory filter and manifest to match version number and code
    def accessoryFile = file("res/xml/accessory_filter.xml")
    def accessoryText = accessoryFile.getText()

    def firstLine = accessoryText.substring(0, accessoryText.indexOf('>')+1) // We need to skip the first line in the xml file
    accessoryText = accessoryText.substring(accessoryText.indexOf('>')+1)

    println "Updating accessory filter version to: " + project.versionName
    def matcherVersionName = Pattern.compile("version=\"(.*?)\"").matcher(accessoryText)
    accessoryText = matcherVersionName.replaceAll("version=\"" + project.versionName + "\"")

    accessoryFile.write(firstLine + accessoryText)
}

apply plugin: 'android'

android {
    compileSdkVersion 18
    buildToolsVersion '18.0.1'

    defaultConfig {
        targetSdkVersion 18
        versionName = project.versionName
    }

    productFlavors {
        standard {
            minSdkVersion 10
            versionCode = Integer.parseInt('1000' + project.versionCode)
        }
        usb {
            minSdkVersion 12
            versionCode = Integer.parseInt('1200' + project.versionCode)
        }
    }

    signingConfigs {
        release
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
        }
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
        standard {
            java.srcDirs = ['standard/src']
            res.srcDirs = ['standard/res']
        }
        usb {
            manifest.srcFile 'usb/AndroidManifest.xml'
            java.srcDirs = ['usb/src']
            res.srcDirs = ['usb/res']
            assets.srcDirs = ['usb/assets']
        }
    }
}

repositories {
    mavenCentral()
    maven { url 'libs' }
}

dependencies {
    compile 'com.android.support:support-v4:18.0.+'
    compile 'com.jjoe64.graphview:graphview:3.1'
    compile 'com.viewpagerindicator:viewpagerindicator:2.4.1'
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    usbCompile 'com.physicaloid:physicaloid:1.0' // Only supported on API level above 12
}

// These values are all set in my global gradle.properties file
if (project.hasProperty('storeFile') && project.hasProperty('storePassword') && project.hasProperty('keyAlias') && project.hasProperty('keyPassword')) {
    android.signingConfigs.release.storeFile = file(storeFile)
    android.signingConfigs.release.storePassword = storePassword
    android.signingConfigs.release.keyAlias = keyAlias
    android.signingConfigs.release.keyPassword = keyPassword
} else {
    android.buildTypes.release.signingConfig = null
}

// Releasing:
// 1. Update versionName and versionCode
// 2. Run 'gradle clean build'
// 3. Move APK to releases directory
// 4. Upload to Github and create tag
// 5. Upload to Google Play Store
//    * Also take new screenshots if necessary